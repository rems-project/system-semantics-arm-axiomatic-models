"VMSA strong"
include "cos.cat"
include "arm-common.cat"

(* observed by *)
let obs = rfe | fr | co

(* dependency-ordered-before *)
let dob =
    addr | data
  | ctrl; [W]
  |(ctrl | (addr; po)); [ISB]; po; [R]
  | (ctrl | (addr; po; [W]))
  | (addr | data); rfi

(* atomic-ordered-before *)
let aob = rmw
  | [range(rmw)]; rfi; [A | Q]

(* barrier-ordered-before *)
let bob = [R] ; po ; [dmbld]
  | [W] ; po ; [dmbst]
  | [dmbst]; po; [W]
  | [dmbld]; po; [R|W]
  | [L]; po; [A]
  | [A | Q]; po; [R | W]
  | [R | W]; po; [L]

(* Ordered-before *)
let ob1 = obs | dob | aob | bob
let ob = ob1^+

(* Internal visibility requirement *)
acyclic po-loc | fr | co | rf as internal

(* External visibility requirement *)
irreflexive ob as external

(* Atomic: Basic LDXR/STXR constraint to forbid intervening writes. *)
empty rmw & (fre; coe) as atomic